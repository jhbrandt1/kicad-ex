* MAX9203 MACROMODEL
* ------------------------------
* Revision 0, 8/2014
* ------------------------------
* Model Platform: Pspice
*-------------------------------
.SUBCKT MAX9203 VCC IN+ IN- VEE VDD OUT GND LE
 XCOMP VCC IN+ IN- VEE VDD OUT GND LE COMP
.ENDS
***************************
***************************
.SUBCKT COMP VDD 1 2 5a 8a 6 GND LE
*******************************
VG1 G1 5 0V
VRG1 V1 G1 1V
VRG2 V5 G1 0.5V
VRG3 V2 G1 2V
V5a 5a 5 0
F5a 5a 0 V5a -1
Ivee 0 5a 1.1m
Gvee 0 5a 5a 0 0.0333333m
V8a 8a 8 0
F8a 8a 0 V8a -1
Ivdd 8a 0 0.35m
Gvdd 8a 0 8a 0 0.0333333m
*******************************
*INPUT STAGE
GBIAS 8 IN1 VALUE={V(V1,SH1)*0.21M}
QIN1 IN2 IN4 IN1 QP1
QIN2 IN3 IN6 IN1 QP1
RIN1 IN2 G1 0.1K
RIN2 IN3 G1 0.1K
EPSR IN4 IN5 VALUE={V(8,G1)*0.05M}
ECMR IN7 IN5 VALUE={V(IN1,G1)*1U}
VOS IN7A IN7 1M
CIN1 IN4 G1 3P
CIN2 IN6 G1 3P
CPD IN2 IN3 10.3P
VIL CM1 G1 -6
EIL CM2 G1 VALUE={V(8,G1)+0.75}
DIL1 CM1 IN1 DA
DIL2 IN1 CM2 DA
********
*IN PROTECTION
RIN3 1 IN7A 200
RIN4 2 IN6 200
DI1 IN7 IN10 DB
DI2 IN10 IN11 DB
DI3 IN11 IN6 DB
DI4 IN6 IN8 DB
DI5 IN8 IN9 DB
DI6 IN9 IN7 DB
*****************
R35 3 5 0.001
R78 7 5 10k
Rgnd GND 5 0.001
Ele1 LE1 5 POLY(2) LE 5 8 5 5.0 -1.0 0.0 0.0 0.0 
Ele 4 5 LE1 5 0.6
Ile LE 5 0.5u
Ivcc VDD 0 0.95m
Gvcc VDD 0 VDD 0 0.0333333m
*****************
*GAIN STAGE
GA1 G1 A1 VALUE={V(IN3,IN2)+V(A1,V5)*12.63M}
RA1 A1 G1 10K
DA2 G1 A1 DA
DA1 A1 V1 DA
CA1 G1 A1 0.2P
EA1 A2 G1 A1 G1 1
RA2 A2 A3 1K
CA2 A3 G1 1.2P
XBUF1 A3 A4 G1 BUFF
.IC V(A1)=0.5 
*****************
*LATCH
XLHIN 4 LH1 G1 8 DG_IN
*1.5NS DELAY
RL1 LH1 L1 1K
CL1 L1 G1 2.143P
XBUF2 L1 L2 G1 BUFF
*****
CL2 LH1 L3 5P
RL2 L3 G1 1.41K
RL3 L5 G1 500
DL1 L5 L3 DA
XBUF4 L3 L4 G1 BUFF
*****
EL1 L6 G1 VALUE={V(A4,G1)*V(L4,G1)}
EL2 L9 G1 VALUE={V(V1,A4)*V(L4,G1)}
XNOR1 L6 L7 L8 G1 V1 NOR
XNOR2 L9 L8 L7 G1 V1 NOR
*****
*5NS DELAY
DLH1 G1 LH2 DA
DLH2 LH2 V1 DA
GLH2 G1 LH2 L2 V5 225U
CLH1 LH2 G1 1P
XBUF3 LH2 LH3 G1 BUFF
*****
EL3 L10 G1 VALUE={V(A4,G1)*V(V1,LH3)+V(L7,G1)*V(LH3,G1)}
EL4 L11 G1 V1 L10 1 
RL4 L10 LQ 170
RL5 L11 LQB 170
CL4 LQ G1 5P
CL5 LQB G1 5P
*****************
*OUTPUTS
EOUT1 OUT1 G1 VALUE={V(LQ,G1)*V(8,G1)}
ROUT1 OUT1 A6 130
DLIM1 Q1 D1 DA
DLIM2 G1 Q1 DA
GOUT1 G1 Q1 VALUE={V(LQ,V5)*1.4M}
CQ1 Q1 G1 0.1P
QO1 8 Q1 A6 QN1 
QO2 G1 Q1 A6 QP1
COUT2 6 G1 4P
*****
EOUT2 OUT2 G1 VALUE={V(LQB,G1)*V(8,G1)}
ROUT2 OUT2 A7 130
DLIM3 QB1 D1 DA
DLIM4 G1 QB1 DA
GOUT2 G1 QB1 VALUE={V(LQB,V5)*1.4M}
CQB QB1 G1 0.1P
QO3 8 QB1 A7 QN1 
QO4 G1 QB1 A7 QP1
COUT1 7 G1 4P
*****
ELIM1 D1 G1 8 G1 1
*****************
*SHDN
XSHIN 3 SH1 G1 8 DG_IN 
ESH1 SH2 G1 VALUE={V(V1,SH1)*V(8,G1)}
ESH2 SH3 G1 VALUE={V(SH1,G1)*V(8,G1)}
MS1 A6 SH2 6 G1 MOSN L=5U W=1M
MS2 A6 SH3 6 8 MOSP L=5U W=1M
MS3 A7 SH2 7 G1 MOSN L=5U W=1M
MS4 A7 SH3 7 8 MOSP L=5U W=1M
*****************
*SUPPLY CURRENTS
ISUP 8 G1 0.271M
GSUP 8 G1 V1 SH1 0.52M
**********************************************************
.MODEL QP1 PNP(IS=16E-15 BF=70)
.MODEL QN1 NPN(IS=16E-15 BF=70)
.MODEL MOSN NMOS(VTO=1.0 KP=88E-4)
.MODEL MOSP PMOS(VTO=-1.0 KP=88E-4)
.MODEL DA D(N=0.1M)
.MODEL DY D(N=10M)
.MODEL DB D(IS=100E-14)
**********************************************************
.ENDS
****************************
.SUBCKT NOR IN1 IN2 OUT G1 V1 
EN1 N1 G1 IN1 G1 1
EN2 N2 G1 IN2 G1 1 
DN1 N1 N3 DA
DN2 N2 N3 DA
RN1 N3 G1 1MEG
EN3 N4 G1 V1 N3 2
RN2 N4 N5 1K
CN1 N5 G1 1.5P
MN1 N6 N5 G1 G1 MOSN L=5U W=1M
RN3 V1 N6 100K
EN4 N9 G1 N6 G1 2
RN4 N9 N7 1K
CN2 N7 G1 1.5P
MN2 OUT N7 G1 G1 MOSN L=5U W=1M
RN5 V1 OUT 100K
.MODEL MOSN NMOS(VTO=1.0 KP=88E-6)
.MODEL DA D(N=0.1M)
.ENDS
****************************
****************************
.SUBCKT DG_IN IN OUT G1 VCC
EREF DG2 G1 VALUE={(V(VCC,G1)*0.5)+0.4}
EHYST DG2 DG3 VALUE={V(DG1,G1)*0.8}
GD1 G1 DG1 IN DG3 10M
DD1 G1 DG1 DA
DD2 DG1 V1 DA
CD1 DG1 G1 1P
VD1 V1 G1 1V
VOUT DG1 OUT 0V
RIN1 IN G1 333.3K
RIN2 VCC IN 333.3K
.MODEL DA D(N=0.1M)
.ENDS
****************************
****************************
.SUBCKT BUFF IN OUT G1
VRG1 V1 G1 1V
VRG2 V5 G1 0.5V
GBU1 G1 BU1 VALUE={V(IN,V5)+(V(BU1,G1)*10M)} 
DBU1 BU1 V1 DA
DBU2 G1 BU1 DA
CBU1 BU1 G1 0.1P
EOUT OUT G1 BU1 G1 1
RBU1 BU1 G1 1MEG
.IC V(BU1)=0.5
.MODEL DA D(N=0.1M)
.ENDS
****************************



* Copyright (c) 2003-2016 Maxim Integrated Products.  All Rights Reserved.